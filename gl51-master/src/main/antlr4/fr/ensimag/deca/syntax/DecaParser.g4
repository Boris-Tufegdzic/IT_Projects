parser grammar DecaParser;

options {
    // Default language but name it anyway
    //
    language  = Java;

    // Use a superclass to implement all helper
    // methods, instance variables and overrides
    // of ANTLR default methods, such as error
    // handling.
    //
    superClass = AbstractDecaParser;

    // Use the vocabulary generated by the accompanying
    // lexer. Maven knows how to work out the relationship
    // between the lexer and parser and will build the
    // lexer before the parser. It will also rebuild the
    // parser if the lexer changes.
    //
    tokenVocab = DecaLexer;

}

// which packages should be imported?
@header {
    import fr.ensimag.deca.tree.*;
    import java.io.PrintStream;
}

@members {
    @Override
    protected AbstractProgram parseProgram() {
        return prog().tree;
    }
}

prog returns[AbstractProgram tree]
@init {                                             //
    ListDeclClass list_c = new ListDeclClass();     // Even with object prog can have no classes
}                                                   //
    : list_classes[list_c] main EOF {
            assert($main.tree != null);
            $tree = new Program(list_c, $main.tree);
            setLocation($tree, $list_classes.start);

        }
    ;

main returns[AbstractMain tree]
    : /* epsilon */ {
            $tree = new EmptyMain();
        }
    | block {
            assert($block.decls != null);
            assert($block.insts != null);
            $tree = new Main($block.decls, $block.insts);
            setLocation($tree, $block.start);
        }
    ;

block returns[ListDeclVar decls, ListInst insts]
    : OBRACE list_decl list_inst CBRACE {
            assert($list_decl.tree != null);
            assert($list_inst.tree != null);
            $decls = $list_decl.tree;
            $insts = $list_inst.tree;
            setLocation($list_decl.tree, $OBRACE);
            setLocation($list_inst.tree, $OBRACE);
        }
    ;

list_decl returns[ListDeclVar tree]
@init   {
            $tree = new ListDeclVar();
        }
    : decl_var_set[$tree]*
    ;

decl_var_set[ListDeclVar l]
    : type list_decl_var[$l,$type.tree] SEMI
    ;

//Completed
list_decl_var[ListDeclVar l, AbstractIdentifier t]
    : dv1=decl_var[$t] {
        $l.add($dv1.tree);
        }
        (COMMA dv2=decl_var[$t] {
            $l.add($dv2.tree);
        }
        )*
    ;

//Completed
decl_var[AbstractIdentifier t] returns[AbstractDeclVar tree]
@init   {
            AbstractInitialization initialization = new NoInitialization();
        }
    : i=ident {
        }
      (EQUALS e=expr {
            initialization = new Initialization($e.tree);
        }
      )? {
            $tree = new DeclVar(t, $i.tree, initialization);
            setLocation($tree, $i.start);
            setLocation(initialization, $i.start);
        }
    ;

//Completed
list_inst returns[ListInst tree]
@init {
            $tree = new ListInst();
            boolean locationDefined = false;
}
    : (inst {
            $tree.add($inst.tree);
            if (!locationDefined) {
                setLocation($tree, $inst.start);
                locationDefined = true;
            }
        }
      )*
    ;

// Completed
inst returns[AbstractInst tree]
    : e1=expr SEMI {
            assert($e1.tree != null);
            $tree = $e1.tree;
        }
    | SEMI {
            $tree = new NoOperation();
            setLocation($tree, $SEMI);
        }
    | PRINT OPARENT list_expr CPARENT SEMI {
            assert($list_expr.tree != null);
            $tree = new Print(false, $list_expr.tree);
            setLocation($tree, $PRINT);
        }
    | PRINTLN OPARENT list_expr CPARENT SEMI {
            assert($list_expr.tree != null);
            $tree = new Println(false, $list_expr.tree);
            setLocation($tree, $PRINTLN);
        }
    | PRINTX OPARENT list_expr CPARENT SEMI {
            assert($list_expr.tree != null);
            $tree = new Print(true, $list_expr.tree);
            setLocation($tree, $PRINTX);
        }
    | PRINTLNX OPARENT list_expr CPARENT SEMI {
            assert($list_expr.tree != null);
            $tree = new Println(true, $list_expr.tree);
            setLocation($tree, $PRINTLNX);
        }
    | if_then_else {
            assert($if_then_else.tree != null);
            $tree = $if_then_else.tree;
        }
    | WHILE OPARENT condition=expr CPARENT OBRACE body=list_inst CBRACE {
            assert($condition.tree != null);
            assert($body.tree != null);
            $tree = new While( $condition.tree, $body.tree);
            setLocation($tree, $body.start);
        }
    | RETURN expr SEMI {
            assert($expr.tree != null);
            // Class Return not finished
            $tree = new Return($expr.tree);
            setLocation($tree, $RETURN);
        }
    ;

//Completed
if_then_else returns[IfThenElse tree]
@init {
        ListInst listElse = new ListInst();
        ListInst newListElse;
        IfThenElse lastIf;
}
    : if1=IF OPARENT condition=expr CPARENT OBRACE li_if=list_inst CBRACE {
            lastIf = new IfThenElse($condition.tree, $li_if.tree, listElse);
            $tree = lastIf;
            setLocation($tree, $if1);
        }
      (ELSE elsif=IF OPARENT elsif_cond=expr CPARENT OBRACE elsif_li=list_inst CBRACE {
            newListElse = new ListInst();
            lastIf = new IfThenElse($elsif_cond.tree, $elsif_li.tree, newListElse);
            setLocation(lastIf, $ELSE);
            listElse.add(lastIf);
            listElse = newListElse;
        }
      )*
      (ELSE OBRACE li_else=list_inst CBRACE {
            lastIf.setElseBranch($li_else.tree);
        }
      )?
    ;

//Completed
list_expr returns[ListExpr tree]
@init   {
            $tree = new ListExpr();
            boolean locationDefined = false;
        }
    : (e1=expr {
            $tree.add($e1.tree);
            if (!locationDefined) {
                setLocation($tree, $e1.start);
                locationDefined = true;
            }
        }
       (COMMA e2=expr {
            $tree.add($e2.tree);
        }
       )* )?
    ;

//Completed
expr returns[AbstractExpr tree]
    : assign_expr {
            assert($assign_expr.tree != null);
            $tree = $assign_expr.tree;
            setLocation($tree, $assign_expr.start);
        }
    ;

//Completed
assign_expr returns[AbstractExpr tree]
    : e=or_expr (
        /* condition: expression e must be a "LVALUE" */ {
            if (! ($e.tree instanceof AbstractLValue)) {
                throw new InvalidLValue(this, $ctx);
            }
        }
        EQUALS e2=assign_expr {
            assert($e.tree != null);
            assert($e2.tree != null);
            $tree = new Assign((AbstractLValue) $e.tree, $e2.tree);
            setLocation($tree, $e.start);
        }
      | /* epsilon */ {
            assert($e.tree != null);
            $tree = $e.tree;
            setLocation($tree, $e.start);
        }
      )
    ;

//Completed
or_expr returns[AbstractExpr tree]
    : e=and_expr {
            assert($e.tree != null);
            $tree = $e.tree;
            setLocation($tree, $e.start);
        }
    | e1=or_expr OR e2=and_expr {
            assert($e1.tree != null);
            assert($e2.tree != null);
            $tree = new Or($e1.tree, $e2.tree);
            setLocation($tree, $e1.start);
       }
    ;

//Completed
and_expr returns[AbstractExpr tree]
    : e=eq_neq_expr {
            assert($e.tree != null);
            $tree = $e.tree;
            setLocation($tree, $e.start);
        }
    |  e1=and_expr AND e2=eq_neq_expr {
            assert($e1.tree != null);                         
            assert($e2.tree != null);
            $tree = new And($e1.tree, $e2.tree);
            setLocation($tree, $e1.start);
        }
    ;

//Completed
eq_neq_expr returns[AbstractExpr tree]
    : e=inequality_expr {
            assert($e.tree != null);
            $tree = $e.tree;
            setLocation($tree, $e.start);
        }
    | e1=eq_neq_expr EQEQ e2=inequality_expr {
            assert($e1.tree != null);
            assert($e2.tree != null);
            $tree = new Equals($e1.tree, $e2.tree);
            setLocation($tree, $e1.start);
        }
    | e1=eq_neq_expr NEQ e2=inequality_expr {
            assert($e1.tree != null);
            assert($e2.tree != null);
            $tree = new NotEquals($e1.tree, $e2.tree);
            setLocation($tree, $e1.start);
        }
    ;

//Completed
inequality_expr returns[AbstractExpr tree]
    : e=sum_expr {
            assert($e.tree != null);
            $tree = $e.tree;
            setLocation($tree, $e.start);
        }
    | e1=inequality_expr LEQ e2=sum_expr {
            assert($e1.tree != null);
            assert($e2.tree != null);
            $tree = new LowerOrEqual($e1.tree, $e2.tree);
            setLocation($tree, $e1.start);
        }
    | e1=inequality_expr GEQ e2=sum_expr {
            assert($e1.tree != null);
            assert($e2.tree != null);
            $tree = new GreaterOrEqual($e1.tree, $e2.tree);
            setLocation($tree, $e1.start);
        }
    | e1=inequality_expr GT e2=sum_expr {
            assert($e1.tree != null);
            assert($e2.tree != null);
            $tree = new Greater($e1.tree, $e2.tree);
            setLocation($tree, $e1.start);
        }
    | e1=inequality_expr LT e2=sum_expr {
            assert($e1.tree != null);
            assert($e2.tree != null);
            $tree = new Lower($e1.tree, $e2.tree);
            setLocation($tree, $e1.start);
        }
    | e1=inequality_expr INSTANCEOF type {
            assert($e1.tree != null);
            assert($type.tree != null);
            $tree = new InstanceOf($type.tree, $e1.tree);
            setLocation($tree, $e1.start);
        }
    ;


//Completed
sum_expr returns[AbstractExpr tree]
    : e=mult_expr {
            assert($e.tree != null);
            $tree = $e.tree;
            setLocation($tree, $e.start);
        }
    | e1=sum_expr PLUS e2=mult_expr {
            assert($e1.tree != null);
            assert($e2.tree != null);
            $tree = new Plus($e1.tree, $e2.tree);
            setLocation($tree, $e1.start);
        }
    | e1=sum_expr MINUS e2=mult_expr {
            assert($e1.tree != null);
            assert($e2.tree != null);
            $tree = new Minus($e1.tree, $e2.tree);
            setLocation($tree, $e1.start);
        }
    ;

//Completed
mult_expr returns[AbstractExpr tree]
    : e=unary_expr {
            assert($e.tree != null);
            $tree = $e.tree;
            setLocation($tree, $e.start);
        }
    | e1=mult_expr TIMES e2=unary_expr {
            assert($e1.tree != null);                                         
            assert($e2.tree != null);
            $tree = new Multiply($e1.tree, $e2.tree);
            setLocation($tree, $e1.start);
        }
    | e1=mult_expr SLASH e2=unary_expr {
            assert($e1.tree != null);                                         
            assert($e2.tree != null);
            $tree = new Divide($e1.tree, $e2.tree);
            setLocation($tree, $e1.start);
        }
    | e1=mult_expr PERCENT e2=unary_expr {
            assert($e1.tree != null);                                                                          
            assert($e2.tree != null);
            $tree = new Modulo($e1.tree, $e2.tree);
            setLocation($tree, $e1.start);
        }
    ;

//Completed
unary_expr returns[AbstractExpr tree]
    : op=MINUS e=unary_expr {
            assert($e.tree != null);
            $tree = new UnaryMinus($e.tree);
            setLocation($tree, $op);
        }
    | op=EXCLAM e=unary_expr {
            assert($e.tree != null);
            $tree = new Not($e.tree);
            setLocation($tree, $op);
        }
    | select_expr {
            assert($select_expr.tree != null);
            $tree = $select_expr.tree;
        }
    ;

select_expr returns[AbstractExpr tree]
    : e=primary_expr {
            assert($e.tree != null);
            $tree = $e.tree;
            setLocation($tree, $e.start);
        }
    | e1=select_expr DOT i=ident {
            assert($e1.tree != null);
            assert($i.tree != null);
            $tree = new Selection($e1.tree, $i.tree);
            setLocation($tree, $e1.start);
        }
        (o=OPARENT args=list_expr CPARENT {
            // we matched "e1.i(args)"
            assert($args.tree != null);
            $tree = new MethodCall($e1.tree, $i.tree, $args.tree);
            setLocation($tree, $DOT);
        }
        | /* epsilon */ {
            // we matched "e.i"
 
        }
        )
    ;

primary_expr returns[AbstractExpr tree]
    : ident {
            assert($ident.tree != null);
            $tree = $ident.tree;
        }
    | m=ident OPARENT args=list_expr CPARENT {
            assert($args.tree != null);
            assert($m.tree != null);
            // $tree = $m.tree;
            AbstractExpr expr = new This();
            $tree = new MethodCall(expr, $m.tree, $args.tree);
            setLocation($args.tree, $OPARENT);
            setLocation($tree, $m.start);
            setLocation(expr, $m.start);
        }
    | OPARENT expr CPARENT {
            assert($expr.tree != null);
            $tree = $expr.tree;
            setLocation($tree, $OPARENT);
        }
    | READINT OPARENT CPARENT {
            $tree = new ReadInt();
            setLocation($tree, $READINT);
        }
    | READFLOAT OPARENT CPARENT {
            $tree = new ReadFloat();
            setLocation($tree, $READFLOAT);
        }
    | NEW ident OPARENT CPARENT {
            assert($ident.tree != null);
            $tree = new New($ident.tree);
            setLocation($tree, $NEW);
        }
    | cast=OPARENT type CPARENT OPARENT expr CPARENT {
            assert($type.tree != null);
            assert($expr.tree != null);
            $tree = new Cast($type.tree, $expr.tree);
            setLocation($tree, $cast);
        }
    | literal {
            assert($literal.tree != null);
            $tree = $literal.tree;
        }
    ;

//Completed
type returns[AbstractIdentifier tree]
    : ident {
            assert($ident.tree != null);
            $tree = $ident.tree;
        }
    ;

literal returns[AbstractExpr tree]
    : INT {
            $tree = new IntLiteral(Integer.valueOf($INT.text));
            setLocation($tree, $INT);
        }
    | FLOAT {
            $tree = new FloatLiteral(Float.valueOf($FLOAT.text));
            setLocation($tree, $FLOAT);
        }
    | STRING {
            $tree = new StringLiteral($STRING.text);
            setLocation($tree, $STRING);
        }
    | TRUE {
            $tree = new BooleanLiteral(true);
            setLocation($tree, $TRUE);
        }
    | FALSE {
            $tree = new BooleanLiteral(false);
            setLocation($tree, $FALSE);
        }
    | THIS {
            $tree = new This();
            setLocation($tree, $THIS);
        }
    | NULL {
            $tree = new NullLiteral();
            setLocation($tree, $NULL);
        }
    ;

//Completed
ident returns[AbstractIdentifier tree]
    : IDENT {
            $tree = new Identifier(getDecacCompiler().symbolTable.create($IDENT.text));
            setLocation($tree, $IDENT);
        }
    ;

/****     Class related rules     ****/

//Completed
list_classes[ListDeclClass tree]
    :
      (c1=class_decl {
            $tree.add($c1.tree);
        }
      )*
    ;

// Completed
class_decl returns[AbstractDeclClass tree]
    : CLASS name=ident superclass=class_extension OBRACE class_body CBRACE {
            assert($name.tree != null);
            assert($class_body.ldm != null);
            assert($class_body.ldf != null);
            $tree = new DeclClass($name.tree, $superclass.tree, 
                                  $class_body.ldf, $class_body.ldm);
            setLocation($tree, $CLASS);
        }
    ;

// Completed
class_extension returns[AbstractIdentifier tree]
    : EXTENDS ident {
            $tree = $ident.tree;
            setLocation($tree, $EXTENDS);
        }
    | /* epsilon */ {
        }
    ;

// Completed
class_body returns [ListDeclMethod ldm, ListDeclField ldf ]
@init { 
    boolean firstM = true;
    boolean firstF = true;
    $ldf = new ListDeclField();
    $ldm = new ListDeclMethod();
}
    : (m=decl_method {
            if (firstM) { 
                firstM = false;
                $ldm.add($m.tree);
                setLocation($ldm, $m.start);
            } else $ldm.add($m.tree);  
        }
      | decl_field_set[$ldf] { 
            if (firstF) { 
                firstF = false;
                setLocation($ldf, $decl_field_set.start);
            }
        }
      )*
    ;
// Completed
decl_field_set[ListDeclField l]
    : v=visibility t=type list_decl_field[$v.visi, $t.tree, l] SEMI
    ;

// Completed
visibility returns [Visibility visi]
    : /* epsilon */ {
            $visi = Visibility.PUBLIC;
        }
    | PROTECTED {
            $visi = Visibility.PROTECTED;
        }
    ;

// Completed
list_decl_field[Visibility v, AbstractIdentifier t, ListDeclField l] 
    : dv1=decl_field[$v, $t] {
        $l.add($dv1.tree);
    }
        (COMMA dv2=decl_field[$v, $t] {
            $l.add($dv2.tree);
        }
      )*
    ;

// Completed
decl_field[Visibility v, AbstractIdentifier t] returns [AbstractDeclField tree]
@init { 
        AbstractInitialization initialization = new NoInitialization();
}
    : i=ident {
        }
      (EQUALS e=expr {
            assert($e.tree != null);
            initialization = new Initialization($e.tree);
            setLocation(initialization, $EQUALS);
        }
      )? {
            assert($ident.tree != null);
            $tree = new DeclField($t, $i.tree, $v, initialization);
            setLocation($tree, $i.start);
        }
    ;

// Completed
decl_method returns [AbstractDeclMethod tree]
@init { 
        AbstractMain main = new EmptyMain();
        AbstractMethodBody body = new MethodBody(main);
        ListDeclParam l = new ListDeclParam();
}
    : type ident OPARENT params=list_params[l] CPARENT (block {
            assert($block.decls != null);
            assert($block.insts != null);
            main = new Main($block.decls, $block.insts);
            body = new MethodBody(main);
            setLocation(main, $block.start);
            setLocation(body, $CPARENT);
        }
      | ASM OPARENT code=multi_line_string CPARENT SEMI {
            assert($code.text != null);
            StringLiteral sct = new StringLiteral($code.text);
            body = new MethodAsmBody(sct);
            setLocation(sct, $OPARENT);
            setLocation(body, $OPARENT);
        }
      ) {
            assert($type.tree != null);
            assert($ident.tree != null);
            assert(l != null);

            $tree = new DeclMethod($type.tree, $ident.tree, l, body);
            
            setLocation($type.tree, $type.start);
            setLocation($ident.tree, $ident.start);
            setLocation(l, $OPARENT);
            setLocation($tree, $type.start);
        }
    ;

// Completed
list_params [ListDeclParam l] 
    : (p1=param {
            $l.add($p1.tree);
        } (COMMA p2=param {
                l.add($p2.tree);
        }
      )*)?
    ;
    
multi_line_string returns[String text, Location location]
    : s=STRING {
            $text = $s.text;
            $location = tokenLocation($s);
        }
    | s=MULTI_LINE_STRING {
            $text = $s.text;
            $location = tokenLocation($s);
        }
    ;

// Completed
param returns [AbstractDeclParam tree]
    : type ident {
            assert($type.tree != null);
            assert($ident.tree != null);
            $tree = new DeclParam($type.tree, $ident.tree);
            setLocation($tree, $type.start);
        }
    ;
